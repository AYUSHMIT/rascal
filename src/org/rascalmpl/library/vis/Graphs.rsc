@license{
  Copyright (c) 2022 CWI
  All rights reserved. This program and the accompanying materials
  are made available under the terms of the Eclipse Public License v1.0
  which accompanies this distribution, and is available at
  http://www.eclipse.org/legal/epl-v10.html
}
@contributor{Jurgen J. Vinju - Jurgen.Vinju@cwi.nl - CWI}
@synopsis{Simple data visualization using graphs}
@description{
This modules provides a simple API to create graph for Rascal
(relational) data, based on [Cytoscape.js](https://js.cytoscape.org/). 

This module is quite new and may undergo some tweaks in the coming time.
}
@examples{
```rascal-shell
import vis::Graphs;
graph([<x,x+1> | x <- [1..100]] + [<100,1>])
```
}
@benefits{
* Easy to use for basic graph layouts.
}
module vis::Graphs

import lang::html::IO;
import Content;
import Set;
import List;

@synopsis{A scatterplot from a binary numerical relation.}

Content graph(lrel[str x, str y] v, str title="Graph") 
    = content(title, graphServer(cytoscape(
        elements=[ *[\node(e) | e <- {*v<x>, *v<y>}],
                   *[\edge(from,to) | <from,to> <- v]
        ],
        style=[
            cytostyle(
                selector=\node(),
                style=(
                    "background-color" : "#666",
                    "label" : "data(id)"
                )
            ),
            cytostyle(
                selector=\edge(),
                style=(
                    "width": "3",
                    "line-color": "#ccc",
                    "target-arrow-color": "#ccc",
                    "target-arrow-shape": "triangle",
                    "curve-style" : "bezier"
                )
            )
        ],
        \layout=cytolayout(
            name="grid",
            rows=2
        )
    )));


data Cytoscape 
    = cytoscape(
        list[CytoElement] elements = [],
        list[CytoStyle] style=[],
        CytoLayout \layout = cytolayout()
    );

data CytoElement 
    = \node(str id)
    | \edge(str source, str target, str id="")
    ;

data CytoStyle
    = cytostyle(
        CytoSelector selector = \node(),
        map[str,str] style = ()
    );

data CytoSelector
    = \node()
    | \edge()
    ; 

data CytoLayout
    = cytolayout(
        str name = "grid",
        int rows = 2
    );

@synopsis{this is the main server generator for any graph value}
@description{
Given a Graph value this server captures the value and serves it
as a JSON value to the HTML client generated by ((plotHTML)).
}
Response (Request) graphServer(Cytoscape ch) {
    Response reply(get(/^\/cytoscape/)) {
        return response(ch);
    }

    // returns the main page that also contains the callbacks for retrieving data and configuration
    default Response reply(get(_)) {
        return response(writeHTMLString(plotHTML()));
    }

    return reply;
}

@synopsis{default HTML wrapper for a chart}
private HTMLElement plotHTML()
    = html([
        div([ // put div here instead of `head` to work around issue
            script([], src="https://cdnjs.cloudflare.com/ajax/libs/cytoscape/3.23.0/cytoscape.min.js")
        ]),
        body([
            div([], id="visualization"),
            script([
                \data(
                    "var container = document.getElementById(\'visualization\');
                    'fetch(\'/cytoscape\').then(resp =\> resp.json()).then(cs =\> {
                    '   cs[\'container\'] = container; 
                        var cytoscapeAPI = cytoscape(cs);  
                    '});
                    '")
            ], \type="text/javascript")
        ], style="position: fixed; top:50%; left:50%; transform: translate(-50%, -50%); width:min(75%,800px);")
    ]);
